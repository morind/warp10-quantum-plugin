import java.text.SimpleDateFormat

plugins {
  id 'com.github.johnrengelman.shadow' version '1.2.3'
  id "com.jfrog.bintray" version "1.4"
}

apply plugin: 'idea'
apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'maven-publish';
apply plugin: 'com.jfrog.bintray'

archivesBaseName = 'warp10-quantum-plugin'
def quantumVersion = '2.6.7'
version = quantumVersion
group = 'io.warp10'

sourceCompatibility = 1.7
targetCompatibility = 1.7

//
// Repositories for dependency resolution
//
repositories {
  jcenter()
  mavenCentral()
  mavenLocal()
  maven {
    url 'https://repository.apache.org/content/groups/public'
  }
  maven {
    url "http://maven.twttr.com"
  }

  maven {
    url 'https://dl.bintray.com/cityzendata/maven'
  }

  maven {
    url 'https://dl.bintray.com/cityzendata/generic'
  }

  maven {
    url 'https://dl.bintray.com/hbs/maven'
  }

  maven {
    url "http://nexus.bedatadriven.com/content/groups/public/"
  }
}

configurations {
  provided
}

dependencies {
  provided group:'io.warp10', name: 'warpscript', version: '1.2.11-rc2'
  compile group:'io.warp10', name: 'warp10-quantum-server', version: quantumVersion
}

sourceSets {
  main {
    compileClasspath += configurations.provided
  }
}

idea {
  module {
    inheritOutputDirs = true
    scopes.PROVIDED.plus += [configurations.provided]
  }
}

shadowJar {
  manifest {
    attributes(
        'Main-Class': 'io.warp10.quantum.Main'
    )
  }
  relocate 'org', 'io.warp10.quantum.org'
  relocate 'jetty', 'io.warp10.quantum.jetty'
  relocate 'javax', 'io.warp10.quantum.javax'
  classifier = null
}

publishing {
  publications {
    QuantumPlugin(MavenPublication) {
      artifact shadowJar {
        extension = 'jar'
      }
      groupId 'io.warp10'
      artifactId archivesBaseName
      version quantumVersion
    }
  }
}

// DEPLOY ON BINTRAY
bintray {
  user = project.hasProperty('bintrayUser') ? project.property('bintrayUser') : System.getenv('BINTRAY_USER')
  key = project.hasProperty('bintrayApiKey') ? project.property('bintrayApiKey') : System.getenv('BINTRAY_API_KEY')

  dryRun = false

  publications = ['QuantumPlugin']

  pkg {
    repo = 'generic'
    name = archivesBaseName
    licenses = ['Apache-2.0']
    vcsUrl = 'https://github.com/cityzendata/warp10-quantum-plugin.git'
    version {
      name = quantumVersion
      released  = new SimpleDateFormat("yyyy-MM-dd'T'HH:mm:ss.SSSZZ").format(new Date())
      vcsTag = quantumVersion
    }
  }
}

artifacts {
  archives shadowJar
}
